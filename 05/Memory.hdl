// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

// First Ram16K  : 16383 -> 000000000000000
// Last Ram16K   : 16383 -> 011111111111111
// First Screen  : 16384 -> 100000000000000
// Last Screen   : 24575 -> 101111111111111
// Keyboard      : 24576 -> 110000000000000

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    Nand(a=address[14], b=address[13], out=isLoadable);
    And(a=load, b=isLoadable, out=toLoad);
    DMux(in=toLoad, sel=address[14], a=r0, b=r1);

    RAM16K(in=in, load=r0, address=address[0..13], out=out0);
    Screen(in=in, load=r1, address=address[0..12], out=out1);
    Keyboard(out=out2);

    Mux16(a=out1, b=out2, sel=address[13], out=screenOrKbd);
    Mux16(a=out0, b=screenOrKbd, sel=address[14], out=out);
}